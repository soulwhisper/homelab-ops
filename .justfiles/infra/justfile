set quiet := true
set shell := ['bash', '-euo', 'pipefail', '-c']

BUCKETS := 'postgres volsync'
AWS_ENDPOINT_URL := env_var_or_default('AWS_ENDPOINT_URL', 'http://localhost:9000')

[private]
default:
  @just --list

[doc('Init Archlinux workstation')]
workstation:
  @echo "Install pacman packages"
  pacman -Syuq --needed --noconfirm --noprogressbar $(cat Archfile | xargs)
  @echo "Install yay packages"
  yay -Syuq --needed --noconfirm --noprogressbar $(cat Yayfile | xargs)
  @echo "Install krew packages"
  kubectl krew index add netshoot https://github.com/nilic/kubectl-netshoot.git
  kubectl krew install $(cat Krewfile | xargs)
  go install github.com/mirceanton/kubectl-switch/v2@latest
  @echo "Set fish aliases"
  fish -i -c "fish alias --save k=kubectl"
  fish -i -c "fish alias --save kc=kubectl-switch context"
  fish -i -c "fish alias --save kns=kubectl-switch ns"
  fish -i -c "fish alias --save ks=kubescape"
  fish -i -c "fish alias --save kubectl=kubecolor"
  fish -i -c "fish alias --save flux-local='uvx flux-local'"
  @echo "Workstation setup complete"

[doc('Init/Reset Garage'), no-exit-message]
garage mode:
  for BUCKET in {{BUCKETS}}; do \
    if [ "{{mode}}" = "init" ]; then \
      access_key=$(op item get garage --fields "${BUCKET}_access_key" --reveal); \
      secret_key=$(op item get garage --fields "${BUCKET}_secret_key" --reveal); \
      garage bucket create "${BUCKET}"; \
      garage key import "$access_key" "$secret_key" -n "${BUCKET}" --yes; \
      garage bucket allow --read --write "${BUCKET}" --key "${BUCKET}"; \
      echo "Created bucket: ${BUCKET}"; \
    elif [ "{{mode}}" = "reset" ]; then \
      garage bucket delete "${BUCKET}"; \
      echo "Deleted bucket: ${BUCKET}"; \
    else \
      echo "Unknown mode: {{mode}}"; \
      exit 1; \
    fi; \
  done

[doc('Init/Reset Versity S3 Gateway'), no-exit-message]
versity mode:
  export AWS_ACCESS_KEY_ID=$(op item get app_user --fields admin_user --reveal); \
  export AWS_SECRET_ACCESS_KEY=$(op item get app_user --fields admin_pass --reveal); \
  for BUCKET in {{BUCKETS}}; do \
    if [ "{{mode}}" = "init" ]; then \
      aws --endpoint-url "{{AWS_ENDPOINT_URL}}" s3api create-bucket --bucket "${BUCKET}" || true; \
      echo "Created bucket: ${BUCKET}"; \
    elif [ "{{mode}}" = "reset" ]; then \
      aws --endpoint-url "{{AWS_ENDPOINT_URL}}" s3api delete-bucket --bucket "${BUCKET}" || true; \
      echo "Deleted bucket: ${BUCKET}"; \
    else \
      echo "Unknown mode: {{mode}}"; \
      exit 1; \
    fi; \
  done
